;****************************************
; Blower
; 1996-2022
; Amiga version
; Author: Youen CHENE
;
; Palette :
;  0-1 World (Ground - Non Blocking)
;  2-3 World (Wall - Blocking)
;  4-15 Player
;  17-19 Bullets
;  21-23 Bullets
;  25-27 Flam Thrower
;  29-31 Flam Thrower
;
;****************************************

WBStartup

#mapWidth=20
#mapHeight=14
#mapSize=280
#initAnimCount=2
#endAnimCount=8
#bulletPerPlayer=2
#waitBullet=30
#speedBullet=1

NEWTYPE .Level
  map.b[#mapSize]
End NEWTYPE 

NEWTYPE .Player
  x.w
  y.w
  dead.b
  direction.b
  animIndex.b
  frameAnim.b
  frameCounter.b
  stepsize.b
  bullets.b
  waitBullet.b
End NEWTYPE 

NEWTYPE .Bullet
  x.w
  x1.w
  x2.w
  y.w
  y1.w
  y2.w
  direction.b
  animIndex.b
  speed.b
  player.b
  init.b
  endLife.b
  channel.b
End NEWTYPE 


Bitmap 0,320,224,4; db0
Bitmap 1,320,224,4; db1
Bitmap 2,320,16,4 ;hid 

Bitmap 3, 80, 64, 4 ;Bobs 
Bitmap 4, 32, 32, 2 ;World 1 
Bitmap 5, 48, 8, 2 ;Bullets Sprite 


NPrint "Load Tiles..."
LoadBitMap 4, "assets/world1.iff"
; Load Tile
Use Bitmap 4
GetaShape 1,0,0,16,16
GetaShape 3,0,16,16,16
GetaShape 2,16,0,16,16
GetaShape 4,16,16,16,16
Free Bitmap 4



LoadBitMap 3, "assets/cowboy.iff"

; Anim
;0 : shooted animation
;1-4 : walk animation

Use Bitmap 3

;top anim
GetaShape 50,64,32,16,16 ;shot
for i.b=0 to 3
  GetaShape 51+i,16*i,32,16,16 ;walk
next

;right anim
GetaShape 60,64,0,16,16 ;shot
for i.b=0 to 3
  GetaShape 61+i,16*i,0,16,16 ;walk
next

;bottom anim
GetaShape 70,64,16,16,16 ;shot
for i.b=0 to 3
  GetaShape 71+i,16*i,16,16,16 ;walk
next

;left anim
for i.b=0 to 4
  CopyShape 60+i,80+i:XFlip 80+i
next

;dead
GetaShape 90,0,48,32,16 ;shot


Free BitMap 3

; Bullet Sprite
NPrint "Load Sprites..."
LoadBitMap 5, "assets/bullet.iff"
Use Bitmap 5
GetaShape 0,0,0,1,1
GetaSprite 0,0

;up & down
GetaShape 0,24,4,7,4
GetaSprite 7,0
YFlip 0
GetaSprite 1,0
GetaShape 0,35,0,2,4
GetaSprite 8,0
YFlip 0
GetaSprite 2,0
GetaShape 0,40,1,6,7
GetaSprite 9,0
YFlip 0
GetaSprite 3,0
;right & left
GetaShape 0,0,0,4,7
GetaSprite 4,0
XFlip 0
GetaSprite 10,0
GetaShape 0,10,3,4,2
GetaSprite 5,0
XFlip 0
GetaSprite 11,0
GetaShape 0,17,1,7,6
GetaSprite 6,0
XFlip 0
GetaSprite 12,0

Free BitMap 5

DEFTYPE .Level level1

NPrint "Load Map..."

dummy.b 
if ReadFile(0,"assets/level1.dat") 
  ReadMem 0,&dummy,1
  ReadMem 0,&dummy,1
  size.w=#mapSize
  ReadMem 0,&level1\map,size
Else
  NPrint "Error reading file."
EndIf
CloseFile 0

NPrint "Draw Map..."
Use Bitmap 0
for x.b=0 to (#mapWidth-1)
    for y.b=0 to (#mapHeight-1)
        tile=level1\map[x+y*#mapWidth]
        Block tile,x*16,y*16
    next
next
CopyBitMap 0,1 ; for double buffering


; HID
Use Bitmap 2
Cls 1


; Palette
NPrint "Init Palette..."
InitPalette 0,32
InitPalette 1,32
LoadPalette 1, "assets/cowboy.iff"
LoadPalette 1, "assets/world1.iff",0
LoadPalette 1, "assets/bullet.pal",16
LoadPalette 1, "assets/bullet.pal",20

InitPalette 2,32
DuplicatePalette 1,2  


Buffer 0,16384
Buffer 1,16384

NPrint "Init CopList..."
flags.l = $04
InitCopList 0,44,16,flags,8,32,0
InitCopList 1,62,224,flags,8,32,0
Delay_ 50

Blitz

CreateDisplay 0,1

;hid
DisplayPalette 0,2
DisplayBitMap 0,2
;main
DisplayPalette 1,0
DisplayBitMap 1,0
DisplayPalette 1,1

nbPlayer.b=1
Dim player.Player(nbPlayer)

player(0)\dead=0
player(0)\x=16
player(0)\y=16
player(0)\direction=1
player(0)\animIndex=0
player(0)\frameAnim=4
player(0)\frameCounter=0
player(0)\stepsize=3
player(0)\bullets=#bulletPerPlayer
player(0)\waitBullet=0

Dim List bullets.Bullet(8)

db.b=0
Repeat
  VWait
  ; manage double buffering
  db=1-db
  Use Bitmap db
  UnBuffer db

  ;
  for i=0 to (nbPlayer-1)
    vJoyx.b=Joyx(1)
    vJoyy.b=Joyy(1)
    If (vJoyx <> 0 OR vJoyy <> 0)
      if vJoyx=1
        player(i)\direction=1
      Endif 
      if vJoyx=-1
        player(i)\direction=3
      EndIf
      if vJoyy=1
        player(i)\direction=2
      EndIf
      if vJoyy=-1
        player(i)\direction=0
      EndIf 
      player(i)\frameCounter=player(i)\frameCounter+1
      If (player(i)\frameCounter=player(i)\frameAnim)
        ; next move
        next_x.w=player(i)\x
        next_y.w=player(i)\y
        ;Debug
        ;BitMapOutput 2 : Colour 2,0
        ;Locate 0,0
        ;Print Point(next_x,next_y)
        ;Locate 2,0
        ;Print Point(next_x+15,next_y)
        ;Locate 4,0
        ;Print Point(next_x+15,next_y+15)
        ;Locate 6,0
        ;Print Point(next_x,next_y+15)
        ;Wall Collision
        auth_move.b=0
        If (player(i)\direction=1)
          If (Point(next_x+16,next_y+1) <> 2 AND Point(next_x+16,next_y+1) <> 3)
            If (Point(next_x+16,next_y+14) <> 2 AND Point(next_x+16,next_y+14) <> 3)
                auth_move=1
            EndIf
          EndIf
        EndIf
        If (player(i)\direction=3)
          If (Point(next_x-1,next_y+1) <> 2 AND Point(next_x-1,next_y+1) <> 3)
            If (Point(next_x-1,next_y+14) <> 2 AND Point(next_x-1,next_y+14) <> 3)
                auth_move=1
            EndIf
          EndIf
        EndIf
        If (player(i)\direction=0)
          If (Point(next_x+3,next_y-1) <> 2 AND Point(next_x+3,next_y-1) <> 3)
            If (Point(next_x+12,next_y-1) <> 2 AND Point(next_x+12,next_y-1) <> 3)
                auth_move=1
            EndIf
          EndIf
        EndIf
        If (player(i)\direction=2)
          If (Point(next_x+3,next_y+16) <> 2 AND Point(next_x+3,next_y+16) <> 3)
            If (Point(next_x+12,next_y+16) <> 2 AND Point(next_x+12,next_y+16) <> 3)
                auth_move=1
            EndIf
          EndIf
        EndIf

        ;Move
        If auth_move 
          player(i)\x=player(i)\x+vJoyx*player(i)\stepsize
          player(i)\y=player(i)\y+vJoyy*player(i)\stepsize
        EndIf

        ;Animation
        player(i)\frameCounter=0
        If (player(i)\animIndex = 3)
          player(i)\animIndex=0
        Else
          player(i)\animIndex=player(i)\animIndex+1
        Endif
      Endif
    Endif
    a.b=51+player(i)\direction*10+player(i)\animIndex

    ; Fire
    If (player(i)\waitBullet>0)
      ; Buffer Joy Button to avoid 2 bullet at the same time 
      player(i)\waitBullet=player(i)\waitBullet-1
    Else
      If (Joyb(1) = 1)
        If (player(i)\bullets>0)
          If AddItem(bullets())
            bullets()\direction=player(i)\direction
            If (player(i)\direction=0)
              bullets()\y=player(i)\y-4    
            EndIf
            If (player(i)\direction=1)
              bullets()\x=player(i)\x+15 
            EndIf
            If (player(i)\direction=2)
              bullets()\y=player(i)\y+15   
            EndIf
            If (player(i)\direction=3)
              bullets()\x=player(i)\x-4   
            EndIf
            bullets()\y1=player(i)\y+6
            bullets()\y2=player(i)\y+8
            bullets()\x1=player(i)\x
            bullets()\x2=player(i)\x+2
            bullets()\animIndex=0
            bullets()\player=i
            bullets()\speed=#speedBullet
            bullets()\init=#initAnimCount
            bullets()\endLife=-1
            bullets()\channel=2-player(i)\bullets
            player(i)\waitBullet=#waitBullet
            player(i)\bullets=player(i)\bullets-1
          Endif
        Endif
      EndIf
    EndIf

    ;draw bobs
    BBlit 0,a,player(i)\x,player(i)\y 
  next

  ;Update and Draw Sprites
  USEPATH bullets()
  ResetList bullets()
  While NextItem(bullets())
    if (\endLife <> -1)
      \animIndex=2
      \endLife=\endLife-1
    Else
      If (\init > 0)
        \init=\init-1
        \animIndex=0
      Else
        dot.b
        If (\direction=0)
          \y=\y-\speed   
          dot.b=Point(\x2,\y-1)
        EndIf
        If (\direction=1)
          \x=\x+\speed 
          dot.b=Point(\x+6,\y2)
        EndIf
        If (\direction=2)
           \y=\y+\speed   
           dot.b=Point(\x2,\y+6)  
        EndIf
        If (\direction=3)
           \x=\x-\speed 
           dot.b=Point(\x-1,\y2)
        EndIf
        \animIndex=1
        If (dot=2 OR dot=3)
         bullets()\endLife=#endAnimCount
        EndIf
      EndIf
    EndIf
    xb.w
    yb.w
    If (\direction=0 OR \direction=2)
      yb=\y   
      If (\animIndex=1)
        xb=\x2
      Else
        xb=\x1
      EndIf
    EndIf
    If (\direction=1 OR \direction=3)
      xb=\x
      If (\animIndex=1)
        yb=\y2
      Else
        yb=\y1
      EndIf
    EndIf
    DisplaySprite 1,1+\direction*3+\animIndex,xb,yb,\player*2+\channel
    If (\endLife=0)
      DisplaySprite 1,0,\x,\y1,\player*2+\channel
      player(\player)\bullets=player(\player)\bullets+1
      KillItem bullets()  
    Endif

    BitMapOutput 2 : Colour 2,0
    Locate 0,0
    Print player(0)\direction
    Locate 3,0
    Print player(0)\bullets
    Locate 6,0
    Print player(0)\waitBullet
  Wend
  
Until RawStatus($45) ; Escape pressed
Delay_ 50
AMIGA
NPrint "The End."
End
