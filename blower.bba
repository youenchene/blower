;****************************************
; Blower
; 1996-2022
; Amiga version
; Author: Youen CHENE
;
; Palette :
;  1-2 World (Ground - Non Blocking)
;  3-4 World (Wall - Blocking)
;  4-16 Player
;  17-19 Bullets
;  20 Player
;  21-23 Bullets
;  24 Player
;  25-27 Flam Thrower
;  28 Player
;  29-31 Flam Thrower
;
; Shape :
;  0 Buffer shape for GetaSprite
;  1-4 World1
;  11-14 World2
;  21-24 World3
;  26-30 HUD - Player
;  31-34 World4
;  36-38 HUD - Avatar
;  50-90 Avatar 1
;  100-140 Avatar 2
;  150-190 Avatar 3
;  100-140 Avatar 4
;****************************************

;NoCli
WBStartup

DEFTYPE .b

Amiga

#skipIntro=1 ;1 to skip intro

#mapWidth=20
#mapHeight=14
#mapSize=280
#levelsCount=7
#initAnimCount=2
#endAnimCount=8
#bulletPerPlayer=2
#waitBullet=30
#speedBullet=3
#maxEnergy=15;

#ShapeIndexHUDAvatar=36
#ShapeIndexHUDPlayer=27

#wallColor1=3
#wallColor2=4
#avatarColor1=5
#avatarColor2=31

#energyBulletImpactNormal=1
#energyBulletImpact1=15
#energyBulletImpact2=8
energyBulletImpact=#energyBulletImpactNormal
#shotAnim=6


chosenWorld.b=-1
chosenMode.b=0

currentWorld.b=2
currentLevel.b=0

Dim chosenPlayer.b(4)

chosenPlayer(0)=1
chosenPlayer(1)=0
chosenPlayer(2)=-1
chosenPlayer(3)=-1


NEWTYPE .Level
  map.b[#mapSize]
End NEWTYPE 

NEWTYPE .player
  x.w
  y.w
  dead.b
  direction.b
  animIndex.b
  frameAnim.b
  frameCounter.b
  stepsize.b
  bullets.b
  waitBullet.b
  joy.b
  avatar.b
  energy.b
  previousEnergy.b
  shot.b
End NEWTYPE 

NEWTYPE .Bullet
  x.w
  x1.w
  x2.w
  y.w
  y1.w
  y2.w
  direction.b
  animIndex.b
  speed.b
  player.b
  init.b
  endLife.b
  channel.b
  srcAvatar.b
End NEWTYPE 

NEWTYPE .SndEvent
  snd.b
  side.b ; 0 for left 1 for right
  vol.b
End NEWTYPE

Statement PrintText{txt$,txtX.w,txtY.w}
  BLITZ
  counter.b = 0
  textLen.b=Len(txt$)
  Dim display.b(textLen)  
  For i.b = 1 To textLen
      oneString$ = Mid$(txt$, i, 1)
      oneAsc = Asc(oneString$)
      newAsc= oneAsc-48
      if newAsc>17:newAsc=newAsc-7:End If
      if newAsc>36:newAsc=newAsc-32:End If
      Select oneAsc
        Case 32
          newAsc=60
        Case 46
          newAsc=36
        Case 58
          newAsc=40
      End Select
      display(counter) = newAsc
      counter = counter + 1
  Next i
  For i.b = 0 To counter-1
      Blit 250+display(i), txtX+(i*4), txtY
  Next i
End Statement

Statement PaletteWorld{n}
  Select n
    Case 0
      PalRGB 1,1,15,12,6  
      PalRGB 1,2,9,6,1  
      PalRGB 1,3,0,15,3  
      PalRGB 1,4,12,12,12  
    Case 2
      PalRGB 1,1,0,15,3  
      PalRGB 1,2,15,12,6  
      PalRGB 1,3,9,3,0  
      PalRGB 1,4,0,6,3  
    Case 3  
      PalRGB 1,1,9,9,9  
      PalRGB 1,2,6,6,6  
      PalRGB 1,3,12,9,6  
      PalRGB 1,4,9,6,0   
    Default ;also case 1
      PalRGB 1,1,0,6,6  
      PalRGB 1,2,0,6,12  
      PalRGB 1,3,5,5,5  
      PalRGB 1,4,9,9,9  
  End Select
  ;Sprite Colorbullets
  For i=0 to 3
    PalRGB 1,16+i*4+1,0,0,0
    PalRGB 1,16+i*4+2,7,7,7  
    PalRGB 1,16+i*4+3,14,14,14  
  Next  
End Statement


.Intro:
  QAMIGA
  ; Intro @ Loading - Micrix Production
  flags3.l = $02
  InitCopList 0,44,256,flags3,8,4,0

  VWAIT 50
  Blitz
  CreateDisplay 0
  InitPalette 3,4
  InitPalette 4,4
  PalRGB 3,0,0,0,0
  PalRGB 3,1,6,0,9
  PalRGB 3,2,14,13,15
  PalRGB 3,3,3,0,6
  DisplayPalette 0,3
  Bitmap 7, 320, 256, 2

  Use BitMap 7

  BitMapOutput 7 

  If (#skipIntro<>1)
    Colour 1,0
    Locate 11,12
    NPrint "Micrix Production"

    DisplayPalette 0, 4
    DisplayBitMap 0, 7

    For i.b = 0 to 50
      FadePalette 3, 4, i / 50
      DisplayPalette 0, 4
      VWAIT
    Next

    for xp.w = 0 to 67
      Plot 88+xp*2,104,2
      VWAIT
      Plot 88+xp*2,104,0
    next
    VWAIT 2
    Plot 222,92,2
    VWAIT 2
    Plot 222,92,0
    Plot 221,92,2
    Plot 223,92,2
    Plot 222,91,2
    Plot 222,93,2
    VWAIT 2
    Plot 221,92,0
    Plot 223,92,0
    Plot 222,91,0
    Plot 222,93,0
    Plot 220,92,2
    Plot 224,92,2
    Plot 222,90,2
    Plot 222,94,2
    VWAIT 12
    Plot 220,92,3
    Plot 224,92,3
    Plot 222,90,3
    Plot 222,94,3
    VWAIT 10

    For i.b = 0 to 50
      FadePalette 3, 4, 1-i / 50
      DisplayPalette 0, 4
      VWAIT 2
    Next
    Cls 0
  Else 
    Cls 0
    DisplayBitMap 0, 7
  End If

.Loading:
  QAMIGA
  DisplayPalette 0, 3
  Locate 0,14
  loadTextY.w=14*8
  Colour 3,0

  Bitmap 0,320,224,5; db0
  Bitmap 1,320,224,5; db1
  Bitmap 2,320,16,5 ;hud 

  Bitmap 3, 80, 256, 5 ;Bobs 
  Bitmap 4, 32, 32, 3 ;World 1 
  Bitmap 5, 48, 8, 2 ;Bullets Sprite 
  Bitmap 6, 64, 81, 5 ;HUD

  Bitmap 8, 244, 15, 2 ;Font
  LoadBitMap 8, "assets/font01.iff"

  for i.b=0 to 60
      GetaShape 250+i,4*i,5,3,5
  next
  Free Bitmap 8

  Use Bitmap 7
  textX.w=10
  textY.w=100
  text$="Loading Worlds..."
  Delay_ 50
  Blitz
  Gosub WriteText
  QAMIGA

  LoadBitMap 4, "assets/world1.iff"
  ; Load Tile
  Use Bitmap 4
  GetaShape 1,0,0,16,16
  GetaShape 3,0,16,16,16
  GetaShape 2,16,0,16,16
  GetaShape 4,16,16,16,16

  LoadBitMap 4, "assets/world2.iff"
  ; Load Tile
  Use Bitmap 4
  GetaShape 11,0,0,16,16
  GetaShape 13,0,16,16,16
  GetaShape 12,16,0,16,16
  GetaShape 14,16,16,16,16

  LoadBitMap 4, "assets/world3.iff"
  ; Load Tile
  Use Bitmap 4
  GetaShape 21,0,0,16,16
  GetaShape 23,0,16,16,16
  GetaShape 22,16,0,16,16
  GetaShape 24,16,16,16,16

  LoadBitMap 4, "assets/world4.iff"
  ; Load Tile
  Use Bitmap 4
  GetaShape 31,0,0,16,16
  GetaShape 33,0,16,16,16
  GetaShape 32,16,0,16,16
  GetaShape 34,16,16,16,16

  Free Bitmap 4

  Use Bitmap 7
  textX.w=10
  textY.w=106
  text$="Loading Sprites and Bobs..."
  Delay_ 50
  Blitz
  Gosub WriteText
  QAMIGA

  LoadBitMap 3, "assets/avatars.iff"

  ; Anim
  ;0 : shooted animation
  ;1-4 : walk animation
  Use Bitmap 3
  for j.b=0 to 3
    avatarIndex.w=j*50
    yIndex.w=j*64
    ;top anim
    GetaShape avatarIndex+50,64,yIndex+32,16,16 ;shot
    for i.b=0 to 3
      GetaShape avatarIndex+51+i,16*i,yIndex+32,16,16 ;walk
    next

    ;right anim
    GetaShape avatarIndex+60,64,yIndex+0,16,16 ;shot
    for i.b=0 to 3
      GetaShape avatarIndex+61+i,16*i,yIndex+0,16,16 ;walk
    next

    ;bottom anim
    GetaShape avatarIndex+70,64,yIndex+16,16,16 ;shot
    for i.b=0 to 3
      GetaShape avatarIndex+71+i,16*i,yIndex+16,16,16 ;walk
    next

    ;left anim
    for i.b=0 to 4
      CopyShape avatarIndex+60+i,avatarIndex+80+i:XFlip avatarIndex+80+i
    next

    ;dead
    GetaShape avatarIndex+90,0,yIndex+48,32,16 ;shot
  next

  Free BitMap 3

  ; Bullet Sprite

  LoadBitMap 5, "assets/bullet.iff"
  Use Bitmap 5
  GetaShape 0,0,0,1,1
  GetaSprite 0,0

  ;up & down
  GetaShape 0,24,4,7,4
  GetaSprite 7,0
  YFlip 0
  GetaSprite 1,0
  GetaShape 0,35,0,2,4
  GetaSprite 8,0
  YFlip 0
  GetaSprite 2,0
  GetaShape 0,40,1,6,7
  GetaSprite 9,0
  YFlip 0
  GetaSprite 3,0
  ;right & left
  GetaShape 0,0,0,4,7
  GetaSprite 4,0
  XFlip 0
  GetaSprite 10,0
  GetaShape 0,10,3,4,2
  GetaSprite 5,0
  XFlip 0
  GetaSprite 11,0
  GetaShape 0,17,1,7,6
  GetaSprite 6,0
  XFlip 0
  GetaSprite 12,0

  Free BitMap 5

  Dim levels.Level(#levelsCount)
  GoSub LoadLevels

  ; HUD
  Use Bitmap 7
  textX.w=10
  textY.w=118
  text$="Initiating HUD..."
  Delay_ 50
  Blitz
  Gosub WriteText
  QAMIGA


  LoadBitMap 6, "assets/hud.iff"
  Use Bitmap 6
  for i.b=0 to 3
    for j.b=0 to 2
      GetaShape #ShapeIndexHUDAvatar+i+j*4,i*16,j*16,16,16
    Next
    GetaShape #ShapeIndexHUDPlayer+i,i*16,49,16,16
  next
  GetaShape #ShapeIndexHUDPlayer-1,0,65,16,16 ; cpu
  Free Bitmap 6

  ; Palette
  Use Bitmap 7
  textX.w=10
  textY.w=124
  text$="Initiating Palette..."
  Delay_ 50
  Blitz
  Gosub WriteText
  QAMIGA


  InitPalette 0,32
  InitPalette 1,32
  LoadPalette 1, "assets/blower.pal"
  PalRGB 1,0,0,0,0  ;Reset T0 color to black




  InitPalette 2,32
  LoadPalette 2, "assets/hud.iff" 
  PalRGB 2,0,0,0,0  ;Reset T0 color to black


  ; SoundEffects
  Use Bitmap 7
  textX.w=10
  textY.w=130
  text$="Loading Sound Effects..."
  Delay_ 50
  Blitz
  Gosub WriteText
  QAMIGA


  LoadSound 0,"assets/snd/gun1amp.iff"
  LoadSound 1,"assets/snd/gun2amp.iff"
  LoadSound 2,"assets/snd/shotwall.iff"
  LoadSound 3,"assets/snd/shot.iff"
  LoadSound 4,"assets/snd/dead.iff"
  LoadSound 5,"assets/snd/step-sand.iff"
  LoadSound 6,"assets/snd/step-wood.iff"
  LoadSound 7,"assets/snd/step-grass.iff"
  LoadSound 8,"assets/snd/step-road.iff"



  Use Bitmap 7
  textX.w=10
  textY.w=136
  text$="Loading Title Screen..."
  Delay_ 50
  Blitz
  Gosub WriteText
  QAMIGA

    
  Bitmap 8, 320, 128, 5
  LoadBitMap 8, "assets/title.iff"
  
  Bitmap 9, 49, 9, 5
  LoadBitMap 9, "assets/title_elements.iff"
  GetaShape 16,0,0,3,5 ; cursor white
  GetaShape 17,4,0,3,5 ; cursor other
  GetaShape 18,8,0,41,9 ; frame

  Free Bitmap 9

  Bitmap 9, 105, 83, 5
  LoadBitMap 9, "assets/title_neon.iff"
  GetaShape 6,0,0,105,31 ; fire back
  GetaShape 7,0,32,101,24 ; text neon 1
  GetaShape 8,0,58,101,24 ; text neon 2

  Free Bitmap 9

  Bitmap 9, 320, 120, 5

  ;for BBlit
  Buffer 0,16384
  Buffer 1,16384

  Use Bitmap 7
  for i.b = 0 to 50
    Fadepalette 3, 4,1-i / 50
    DisplayPalette 0, 4
    vwait
  next

.TitleScreen:
  flagsTS.l = $05
  flagsTS2.l = $05
  InitCopList 0,44,128,flagsTS,0,32,0
  InitCopList 1,174,120,flagsTS2,0,32,0
  LoadPalette 3, "assets/title.iff" 
  InitPalette 4,32
  LoadPalette 5, "assets/hud.iff"

  VWAIT 50
  Blitz
  CreateDisplay 0,1
  DisplayPalette 0, 4
  DisplayPalette 1, 0
  DisplayBitMap 0, 8
  DisplayBitMap 1, 9
  Free BitMap 7

  Use BitMap 8

  Blit 6,107,5 
  Blit 8,108,9 

  Use BitMap 9
  Cls 0

  currentLevel=0

  menuX.w=70
  menuXPadding.w=40
  Dim menuY.w(4)
  menuY(0)=10 
  menuY(1)=30
  menuY(2)=60
  menuY(3)=75

  PrintText{"Start",menuX,menuY(0)}

  PrintText{"Players",menuX,menuY(1)}
  PrintText{"Player 1",menuX+menuXPadding,menuY(1)-10}
  PrintText{"Player 2",menuX+2*menuXPadding,menuY(1)-10}
  PrintText{"Player 3",menuX+3*menuXPadding,menuY(1)-10}
  PrintText{"Player 4",menuX+4*menuXPadding,menuY(1)-10}


  PrintText{"Mode",menuX,menuY(2)}
  PrintText{"Normal",menuX+menuXPadding,menuY(2)}
  PrintText{"One Shot",menuX+2*menuXPadding,menuY(2)}
  PrintText{"Two Shots",menuX+3*menuXPadding,menuY(2)}

  PrintText{"World",menuX,menuY(3)}
  PrintText{"Random",menuX+menuXPadding,menuY(3)}
  PrintText{"Western",menuX+2*menuXPadding,menuY(3)}
  PrintText{"Manor",menuX+3*menuXPadding,menuY(3)}
  PrintText{"Forest",menuX+4*menuXPadding,menuY(3)}
  PrintText{"City",menuX+5*menuXPadding,menuY(3)}

  For i.b = 0 to 50
    FadePalette 3, 4, i / 50
    DisplayPalette 0, 4
    FadePalette 5, 0, i / 50
    DisplayPalette 1, 0
    VWAIT
  Next


  playerCursor.b=0
  cursorB.b=0
  soundM.b=0
  mi.b=0
  actionBuffer=0
  #actionBuf=6
  titleBuffer=4
  titleNeon=1
  Repeat
    VWAIT
    UnBuffer 0
    cursorB=0
    soundM=0
    If (titleBuffer>0)
      titleBuffer=titleBuffer-1
    Else
      Use BitMap 8
      Blit 7+titleNeon,108,9 
      titleNeon=1-titleNeon
      titleBuffer=Rnd(100)
      Use BitMap 9
    End If
    If (actionBuffer > 0)
      actionBuffer=actionBuffer-1
      cursorB=1
    Else
      If (Joyy(1) = 1)
        mi=mi+1
        If mi>3:mi=0:End If
        actionBuffer=#actionBuf
        soundM=1
      End If
      If (Joyy(1)) = -1
        mi=mi-1
        If (mi<0):mi=3:End If
        actionBuffer=#actionBuf
        soundM=1
      End If
      dir.b=Joyx(1)
      btn.b=Joyb(1)
      If (btn <> 0 OR dir.b <> 0)
        If (dir=0):dir=1:End If
        cursorB=1
        soundM=1
        Select mi
          Case 0
            If (btn) 
              Goto Game
              actionBuffer=#actionBuf
            Else
              cursorB=0
              soundM=0
            End If
          Case 1
            If (btn)
              chosenPlayer(playerCursor)=chosenPlayer(playerCursor)+1
              If (chosenPlayer(playerCursor)>3):chosenPlayer(playerCursor)=-1:End If
              For i.b=0 to 3 
                If (i <> playerCursor AND chosenPlayer(i)<>-1) 
                  If (playerCursor=0) ; player 1 can overrride
                    If (chosenPlayer(playerCursor)=chosenPlayer(i))
                      chosenPlayer(i)=-1
                    End If
                  Else
                    If (chosenPlayer(playerCursor)=chosenPlayer(i))
                      chosenPlayer(playerCursor)=chosenPlayer(playerCursor)+1
                      If (chosenPlayer(playerCursor)>3):chosenPlayer(playerCursor)=-1:End If
                    End If
                    For j.b=0 to i-1 
                        If (i <> j AND i <> playerCursor and j <> playerCursor AND chosenPlayer(j)<>-1) 
                          If (chosenPlayer(playerCursor)=chosenPlayer(j))
                            chosenPlayer(playerCursor)=chosenPlayer(playerCursor)+1
                            If (chosenPlayer(playerCursor)>3):chosenPlayer(playerCursor)=-1:End If
                          End If
                        End If
                      Next
                  End If
                End If
              Next
              actionBuffer=#actionBuf
            Else
              playerCursor=playerCursor+dir
              If (playerCursor>3):playerCursor=0:End If
              If (playerCursor<0):playerCursor=3:End If
              actionBuffer=#actionBuf
            End If
          Case 2
            chosenMode=chosenMode+dir
            If (chosenMode>2):chosenMode=0:End If
            If (chosenMode<0):chosenMode=2:End If
            actionBuffer=#actionBuf
          Case 3
            chosenWorld=chosenWorld+dir
            If (chosenWorld>3):chosenWorld=-1:End If
            If (chosenWorld<-1):chosenWorld=3:End If
            actionBuffer=#actionBuf
        End Select
      End If
      If soundM=1:Sound 1,%1100,64:End If
    End If
    ;Update Display
    paddingX.w=0
    If (mi=1):paddingX=menuXPadding*(playerCursor+1):End If
    BBlit 0,16+cursorB,menuX-6+paddingX,menuY(mi)
    BBlit 0,18,menuX+menuXPadding*(chosenMode+1)-4,menuY(2)-2
    BBlit 0,18,menuX+menuXPadding*(chosenWorld+2)-4,menuY(3)-2
    For i.b=0 to 3
      cjoy.b=chosenPlayer(i)
      If (cjoy >= 0 and cjoy < 2)
        cjoy=1-cjoy ; invert because of port 2 as joy port on amiga
      EndIf
      BBlit  0,#ShapeIndexHUDPlayer+cjoy,menuX+menuXPadding*(i+1)+8,menuY(1)
    Next
  Until (RawStatus($45))
  For i.b = 0 to 50
    FadePalette 3, 4,1-i / 50
    DisplayPalette 0, 4
    FadePalette 5, 0,1-i / 50
    DisplayPalette 1, 0
    VWAIT
  Next
  VWAIT 50
  AMIGA
  End

.Game:

  Blitz
  For i.b = 0 to 50
    FadePalette 3, 4,1-i / 50
    DisplayPalette 0, 4
    FadePalette 5, 0,1-i / 50
    DisplayPalette 1, 0
    VWAIT
  next

  QAMIGA
  ;Init CopList
  flags0.l = $05
  flags1.l = $05
  InitCopList 0,44,16,flags0,8,32,0
  InitCopList 1,62,224,flags1,8,32,0

  VWAIT 50
  Blitz
  ;Init data
  ;Init player
  nbPlayer.b=4
  Dim player.player(nbPlayer)

  For i.b=0 to (nbPlayer-1)
    cp.b=i
    Gosub InitPlayer
  Next

  ;Game Mode  
  Select chosenMode
    Case 1
      energyBulletImpact=#energyBulletImpact1
    Case 2
      energyBulletImpact=#energyBulletImpact2
    Default 
      energyBulletImpact=#energyBulletImpactNormal
  End Select
 
  remainingLevels.b=#levelsCount
  Repeat
    ;Init data
    For i.b=0 to (nbPlayer-1)
      UsePath player(i)
      cp.b=i
      Gosub InitPlayer
      Select i
      Case 0
        \dead=0
        \x=16
        \y=16
        \direction=1
        \joy=chosenPlayer(0)
        \avatar=0
      Case 1
        \dead=0
        \x=288
        \y=192
        \direction=3
        \joy=chosenPlayer(1)
        \avatar=1
      Case 2
        \dead=0
        \x=288
        \y=16
        \direction=3
        \joy=chosenPlayer(2)
        \avatar=2
      Case 3
        \dead=0
        \x=16
        \y=192
        \direction=1
        \joy=chosenPlayer(3)
        \avatar=3
      End Select
    Next

    ;Init HUD
    Use Bitmap 2
    Cls 0
    for i=0 to (nbPlayer-1)
      avatar.b=player(i)\avatar
      cjoy.b=player(i)\joy
      If (cjoy >= 0 and cjoy < 2)
        cjoy=1-cjoy ; invert because of port 2 as joy port on amiga
      EndIf
      Block #ShapeIndexHUDPlayer+cjoy,avatar*64,0
      Block #ShapeIndexHUDAvatar+avatar,16+avatar*64,0
      Boxf 32+avatar*64,15-player(i)\energy,48+avatar*64,15,30
    next

    textX.w=256
    textY.w=9
    text$="Level: "+str$(currentLevel+1)
    Gosub WriteText

    remainingPlayers.b=nbPlayer

    currentWorld=chosenWorld
    If (chosenWorld=-1):currentWorld=Rnd(4):End If

    GoSub BuildWorld
    ;World Color
    PaletteWorld{currentWorld}

    CreateDisplay 0,1

    ;hud
    DisplayPalette 0,2
    DisplayBitMap 0,2
    ;main
    DisplayPalette 1,0
    DisplayBitMap 1,0
    DisplayPalette 1,1

    Dim List bullets.Bullet(8)

    Dim List sndevents.SndEvent(8)


    db.b=0 ;double buffering
    Repeat
      Gosub GameLoop
    Until (RawStatus($45) OR remainingPlayers<2) ; Escape pressed
    FlushBuffer 0
    FlushBuffer 1
    currentLevel=currentLevel+1
  Until (RawStatus($45) OR currentLevel>=#levelsCount)
  QAMIGA
  Goto TitleScreen

.InitPlayer:
  UsePath player(cp)
  \dead=0
  \animIndex=0
  \frameAnim=4
  \frameCounter=0
  \stepsize=3
  \bullets=#bulletPerPlayer
  \waitBullet=0
  \energy=#maxEnergy
  \previousEnergy=#maxEnergy
  Return

.BuildWorld:
  QAMIGA
  Use Bitmap 0
  Cls 0
  For x.b=0 to (#mapWidth-1)
      For y.b=0 to (#mapHeight-1)
        tile.b=levels(currentLevel)\map[x+y*#mapWidth]
        Block tile+currentWorld*10,x*16,y*16 ; only amiga mlode?
      Next
  Next
  CopyBitMap 0,1 ; for double buffering
  VWAIT 50
  BLITZ
  Return

.LoadLevels:
  Use Bitmap 7
  textX.w=10
  textY.w=112
  text$="Loading Levels..."
  Delay_ 50
  Blitz
  Gosub WriteText
  QAMIGA

  dummy.b 
  For lev.b=0 to (#levelsCount-1)
    If ReadFile(0,"assets/level"+Str$(lev+1)+".dat") 
      ReadMem 0,&dummy,1
      ReadMem 0,&dummy,1
      size.w=#mapSize
      ReadMem 0,&levels(lev)\map,size
    Else
      NPrint "Error reading file."
    EndIf
    CloseFile 0
  Next
  Return

.GameLoop:
  BLITZ
  VWait
  ; manage double buffering
  db=1-db
  Use Bitmap db
  
  a.w=0


  ;  First process dead so they are at the background
  remainingPlayers=nbPlayer
  For i=0 to (nbPlayer-1)
    UsePath player(i)
    If (\dead=1)
      a=50+\avatar*50+40
      Blit a,\x,\y 
      remainingPlayers=remainingPlayers-1
    EndIf
  Next 

  UnBuffer db

  For i=0 to (nbPlayer-1)
  UsePath player(i)
  If (\dead <> 1)

    a=0
    If (\dead>0 OR \shot>0)
      ;Has been shoted anim
      If (\shot>0)
        a=50+\avatar*50+\direction*10
        \shot=\shot-1
      else
        If (\dead=2)
          \dead=\dead-1
          \x=\x-8
          If (\x>272):\x=272:EndIf
          If AddItem(sndevents())
            sndevents()\snd=4
            If \x >= 160
              sndevents()\side=1
            Else
              sndevents()\side=0
            Endif
          End If
        EndIf
      EndIf
    Else
      currentJoy.b=\joy

      If (currentJoy >= 0  AND currentJoy < 2)
        ;Get Joy Button
        buttonOn=Joyb(currentJoy)
        ; Buffer Joy Button to avoid 2 bullet at the same time 
        If (\waitBullet>0)
          \waitBullet=\waitBullet-1
        EndIf
      EndIf
      ; Interaction
      If (buttonOn=0) ;Move or shoot
        If (currentJoy >= 0  AND currentJoy < 2)
          vJoyx.b=Joyx(currentJoy)
          vJoyy.b=Joyy(currentJoy)
          If (vJoyx <> 0 OR vJoyy <> 0)
            if vJoyx=1
              \direction=1
            Endif 
            if vJoyx=-1
              \direction=3
            EndIf
            if vJoyy=1
              \direction=2
            EndIf
            if vJoyy=-1
              \direction=0
            EndIf 
            \frameCounter=\frameCounter+1
            If (\frameCounter=\frameAnim)
              ; next move
              next_x.w=\x
              next_y.w=\y

              ;Wall Collision
              If (\direction=1)
                pa.w=Point(next_x+16,next_y+1)
                pb.w=Point(next_x+16,next_y+14)
              EndIf
              If (\direction=3)
                pa.w=Point(next_x-1,next_y+1)
                pb.w=Point(next_x-1,next_y+14)
              EndIf
              If (\direction=0)
                pa.w=Point(next_x+3,next_y-1)
                pb.w=Point(next_x+12,next_y-1)
              EndIf
              If (\direction=2)
                pa.w=Point(next_x+3,next_y+16)
                pb.w=Point(next_x+12,next_y+16)
              EndIf

              ;check colors in front of avatar
              paValid.b=(pa <> #wallColor1 AND pa <> #wallColor2)
              pbValid.b=(pb <> #wallColor1 AND pb <> #wallColor2)

              If (paValid=-1 AND pbValid=-1) ;Move
                \x=\x+vJoyx*\stepsize
                \y=\y+vJoyy*\stepsize
              EndIf
              If (paValid=-1 AND pbValid=0) ;Avoid obstacle
                if (\direction=0 OR \direction=2)
                  \x=\x-1
                Else
                  \y=\y-1
                EndIf
              EndIf
              If (paValid=0 AND pbValid=-1) ;Avoid obstacle
                if (\direction=0 OR \direction=2)
                  \x=\x+1
                Else
                  \y=\y+1
                EndIf
              EndIf
              ;Animation
              \frameCounter=0
              If (\animIndex = 3)
                \animIndex=0
                 If AddItem(sndevents())
                  sndevents()\snd=5+currentWorld
                  sndevents()\vol=32
                  If \x >= 160
                    sndevents()\side=1
                  Else
                    sndevents()\side=0
                  Endif
                End If
              Else
                \animIndex=\animIndex+1
              Endif
            Endif
          Endif
        EndIf
      Else
        ; Fire
        \frameCounter=0
        \animIndex=0
        If (\waitBullet<=0)
          If (currentJoy >= 0  AND currentJoy < 2)
            If (buttonOn = 1)
              If (\bullets>0)
                If AddItem(bullets())
                  bullets()\direction=\direction
                  If (\direction=0)
                    bullets()\y=\y-4    
                  EndIf
                  If (\direction=1)
                    bullets()\x=\x+15 
                  EndIf
                  If (\direction=2)
                    bullets()\y=\y+15   
                  EndIf
                  If (\direction=3)
                    bullets()\x=\x-4   
                  EndIf
                  bullets()\y1=\y+6
                  bullets()\y2=\y+8
                  bullets()\x1=\x
                  bullets()\x2=\x+2
                  bullets()\animIndex=0
                  bullets()\player=i
                  bullets()\speed=#speedBullet
                  bullets()\init=#initAnimCount
                  bullets()\endLife=-1
                  bullets()\channel=2-\bullets
                  bullets()\srcAvatar=\avatar
                  \waitBullet=#waitBullet
                  \bullets=\bullets-1
                  If AddItem(sndevents())
                    sndevents()\snd=0
                    If \x >= 160
                      sndevents()\side=1
                    Else
                      sndevents()\side=0
                    Endif
                  End If
                Endif
              Endif
            EndIf
          EndIf
        EndIf
      EndIf
      a=\avatar*50+51+\direction*10+\animIndex
    EndIf
    ;draw bobs
    If (\x<16):\x=16:EndIf
    If (\x>288):\x=288:EndIf
    If (\y<16):\y=16:EndIf
    If (\y>192):\y=192:EndIf
    BBlit 0,a,\x,\y 
  EndIf
  next

  ;Update and Draw Sprites
  USEPATH bullets()
  ResetList bullets()
  While NextItem(bullets())
    dot.b
    if (\endLife <> -1)
      \animIndex=2
      \endLife=\endLife-1
    Else
      If (\init > 0)
        \init=\init-1
        \animIndex=0
      Else
        If (\direction=0)
          \y=\y-\speed   
          dot.b=Point(\x2,\y-1)
        EndIf
        If (\direction=1)
          \x=\x+\speed 
          dot.b=Point(\x+6,\y2)
        EndIf
        If (\direction=2)
           \y=\y+\speed   
           dot.b=Point(\x2,\y+6)  
        EndIf
        If (\direction=3)
           \x=\x-\speed 
           dot.b=Point(\x-1,\y2)
        EndIf
        \animIndex=1
        bound.b=0
        If (\x<16):\x=16:bound=-1:EndIf
        If (\y<16):\y=16:bound=-1:EndIf
        If (\x>304):\x=295:bound=-1:EndIf
        If (\y>208):\y=200:bound=-1:EndIf
        ;Collision with wall
        If (dot=#wallColor1 OR dot=#wallColor2 OR bound.b=-1)
         bullets()\endLife=#endAnimCount
         If AddItem(sndevents())
            sndevents()\snd=2
            If \x >= 160
              sndevents()\side=1
            Else
              sndevents()\side=0
            Endif
          End If
        EndIf
      EndIf
    EndIf
    xb.w
    yb.w
    If (\direction=0 OR \direction=2)
      yb=\y   
      If (\animIndex=1)
        xb=\x2
      Else
        xb=\x1
      EndIf
    EndIf
    If (\direction=1 OR \direction=3)
      xb=\x
      If (\animIndex=1)
        yb=\y2
      Else
        yb=\y1
      EndIf
    EndIf
    ;Collision with avatars
    if (\endLife = -1)
      If (dot>=#avatarColor1 AND dot<=#avatarColor2)
        ;Box xb-7,yb-7,xb-7+14,yb-7+14,8;debug
        For p=0 to (nbPlayer-1)
          If (\srcAvatar <> player(p)\avatar)
            If (player(p)\dead = 0) ; not dead
              ;Box player(p)\x,player(p)\y,player(p)\x+16,player(p)\y+16,9;debug
              If RectsHit(xb-7,yb-7,14,14,player(p)\x,player(p)\y,16,16) ; larger to void more calculus
                bullets()\endLife=#endAnimCount ; End bullet
                player(p)\previousEnergy=player(p)\energy
                player(p)\energy=player(p)\energy-energyBulletImpact
                player(p)\shot=#shotAnim
                If AddItem(sndevents())
                  sndevents()\snd=3
                  If \x >= 160
                    sndevents()\side=1
                  Else
                    sndevents()\side=0
                  Endif
                End If
                If (player(p)\energy <= 0)
                  player(p)\dead=2
                EndIf
              EndIf
            EndIf
          EndIf
        Next
      EndIf
    Endif
    ;Display
    DisplaySprite 1,1+\direction*3+\animIndex,xb,yb,\player*2+\channel
    If (\endLife=0)
      DisplaySprite 1,0,\x,\y1,\player*2+\channel
      player(\player)\bullets=player(\player)\bullets+1
      KillItem bullets()  
    Endif

  Wend

  ;HUD Update
  For p=0 to (nbPlayer-1)
    UsePath player(p)
    If (\energy <> \previousEnergy)
        ahud.b=0
        If (\energy < 10)
          ahud=1
        EndIf
        If (\energy < 4)
          ahud=2
        EndIf
        Use Bitmap 2
        Block #ShapeIndexHUDAvatar+ahud*4+\avatar,16+\avatar*64,0
        Boxf 32+\avatar*64,0,48+\avatar*64,15-\energy,0
        Boxf 32+\avatar*64,15-\energy,48+\avatar*64,15,30 
    EndIf
  Next

  ;Process sounds
  USEPATH sndevents()
  ResetList sndevents()
  prio.b=0
  While NextItem(sndevents())
    If (prio=0)
      If (\side=1)
        side.b=%0100
      Else
        side.b=%1000
      End If
      if (\vol=0): vol.b=64:Else: vol.b=\vol :End If
      Sound \snd,side,vol
    End If
    KillItem sndevents() 
  Wend
  Return

.WriteText:
  counter.b = 0
  textLen.b=Len(text$)
  Dim display.b(textLen)  
  For i.b = 1 To textLen
      oneString$ = Mid$(text$, i, 1)
      oneAsc = Asc(oneString$)
      newAsc= oneAsc-48
      if newAsc>17:newAsc=newAsc-7:End If
      if newAsc>36:newAsc=newAsc-32:End If
      Select oneAsc
        Case 32
          newAsc=60
        Case 46
          newAsc=36
        Case 58
          newAsc=40
      End Select
      display(counter) = newAsc
      counter = counter + 1
  Next i
  Blitz
  For i.b = 0 To counter-1
      Blit 250+display(i), textX+(i*4), textY
  Next i
 Return

